# A vector is a dynamic array, represented by a pointer to a tripe (data, size, capacity)
# Tuples or custom types are not yet supported, so we say it is an int*

fun int*() vecNew() =
    var int* vec = alloc(3);
    vec[0] = 0;
    vec[1] = 0;
    vec[2] = 0;
    return vec;
end

# There is no cast syntax so we convert through a wildcardPtr
fun int**(wildcardPtr) toIntPtrPtr(ptr) =
    return ptr;
end

fun int(int*) vecDelete(vec) =
    free(toIntPtrPtr(vec)[0]);
    free(vec);
end

fun int(int*, int) vecSetCapacity(vec, newCapacity) =
    var int** _vec = toIntPtrPtr(vec);
    if newCapacity < vec[1] then
        exit(-1);
    end
    vec[2] = newCapacity;
    var int* newData = alloc(newCapacity);
    var int i = 0;
    while i < vec[1] do
        newData[i] = _vec[0][i];
        i = i + 1;
    end
    free(_vec[0]);
    _vec[0] = newData;
end

fun int(int*) vecExpand(vec) =
    var int newCapacity = vec[2] * 2;
    if newCapacity == 0 then
        newCapacity = 1;
    end
    vecSetCapacity(vec, newCapacity);
end

fun int(int*, int) vecResize(vec, newSize) =
    if newSize > vec[2] then
        vecSetCapacity(vec, newSize);
    end
    vec[1] = newSize;
end

fun int(int*, int) vecPush(vec, val) =
    var int** _vec = toIntPtrPtr(vec);
    if vec[1] == vec[2] then
        vecExpand(vec);
    end
    _vec[0][vec[1]] = val;
    vec[1] = vec[1] + 1;
end

fun int(int*) vecPop(vec) =
    if vec[1] == 0 then
        exit(-1);
    end
    vec[1] = vec[1] - 1;
end

fun int*(int*, int) vecAt(vec, idx) =
    var int** _vec = toIntPtrPtr(vec);
    if idx < 0 || idx >= vec[1] then
        exit(-1);
    end
    return &_vec[0][idx];
end

fun int*(int*) vecData(vec) =
    return toIntPtrPtr(vec)[0];
end

fun int(int*) vecSize(vec) =
    return vec[1];
end

fun int(int*) vecCapacity(vec) =
    return vec[2];
end

# Example use case: Adjacency list for graph

fun int() main() =
    # Input
    var int n = read();
    var int m = read();
    var int** edges = alloc(n);
    var int i = 0;
    while i < n do
        edges[i] = vecNew();
        i = i + 1;
    end
    i = 0;
    while i < m do
        var int from = read();
        var int to = read();
        vecPush(edges[from], to);
        i = i + 1;
    end

    # Init
    var int* dists = alloc(n);
    i = 0;
    while i < n do
        dists[i] = -1;
        i = i + 1;
    end

    # BFS
    var int* queue = vecNew();
    var int qPos = 0;
    vecPush(queue, 0);
    dists[0] = 0;

    while qPos < vecSize(queue) do
        var int curr = *vecAt(queue, qPos);
        qPos = qPos + 1;
        var int j = 0;
        while j < vecSize(edges[curr]) do
            var int next = *vecAt(edges[curr], j);
            if dists[next] == -1 then
                dists[next] = dists[curr] + 1;
                vecPush(queue, next);
            end
            j = j + 1;
        end
    end

    # Print dists
    i = 0;
    while i < n do
        print(dists[i]);
        i = i + 1;
    end

    #Free memory
    i = 0;
    while i < n do
        vecDelete(edges[i]);
        i = i + 1;
    end
    free(edges);
    free(dists);
    vecDelete(queue);
end

#>

Sample input:

6 8
0 1
0 2
1 2
2 3
3 0
4 1
3 4
2 4

Sample output:

0
1
1
2
2
-1

<#
